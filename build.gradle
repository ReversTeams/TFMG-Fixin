plugins {
    id "eclipse"
    id "idea"
    id "maven-publish"
    id "net.minecraftforge.gradle" version "[6.0,6.2)"
    id "org.spongepowered.mixin" version "0.7.+"
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version_number.toInteger())

println "Java: ${System.getProperty "java.version"}, JVM: ${System.getProperty "java.vm.version"} (${System.getProperty "java.vendor"}), Arch: ${System.getProperty "os.arch"}"
minecraft {
    mappings channel: "official", version: minecraft_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"

            property "forge.logging.console.level", "debug"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property "forge.enabledGameTestNamespaces", mod_id
        }

        server {
            property "forge.enabledGameTestNamespaces", mod_id
            args "--nogui"
        }

        gameTestServer {
            property "forge.enabledGameTestNamespaces", mod_id
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    compileOnly files("libs/create-1.20.1-6.0.8-291-slim.jar")
    compileOnly files("libs/tfmg-1.0.2f.jar")
}

processResources {
    def props = new Properties()
    file("$rootDir/gradle.properties").withInputStream {
        props.load(it) 
    }
    inputs.properties props

    filesMatching([
        "META-INF/*mods.toml",
        "pack.mcmeta"
    ]) {
        expand props
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

tasks.named("jar", Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }

    finalizedBy "reobfJar"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}
